buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:latest.release'
    }
}

plugins {
    id "io.freefair.lombok" version "6.1.0-m3"
    id 'java'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'eclipse'
    id 'idea'
    id 'jacoco'
    id 'antlr'
    id "org.sonarqube" version "3.3"
    id 'com.github.kt3k.coveralls' version '2.12.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

//Need for Windows
def defaultEncoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url 'https://repo.gradle.org/gradle/libs-releases'}
    maven { url 'https://repo.gradle.org/gradle/libs-releases-local' }
}

generateGrammarSource {
    arguments << "-lib" << "src/main/antlr/"
    arguments << "-package" << "com.liferay.damascus.antlr.template"
    outputDirectory = file('src/main/java/com/liferay/damascus/antlr/template')
}

jar {
    manifest {
        attributes(
            "Bundle-SymbolicName": "com.liferay.damascus.cli",
            "Bundle-Version": "1.1.6",
            "Bundle-Description": "Liferay extension tool for scaffolding service builder portlet",
            "Main-Class": "com.liferay.damascus.cli.Damascus",
            "JPM-Command": "damascus"
        )
    }
}

dependencies {
    implementation 'com.beust:jcommander:1.72'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'commons-io:commons-io:2.11.+'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.commons:commons-configuration2:2.7'
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'com.jayway.jsonpath:json-path:2.5.+'

    implementation 'org.freemarker:freemarker:2.3.30'
    implementation 'org.gradle:gradle-tooling-api:6.7.1'
    implementation 'com.liferay:com.liferay.project.templates:5.0.101'

    implementation 'org.slf4j:slf4j-api:1.7.+'
    implementation 'org.slf4j:slf4j-log4j12:1.7.+'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.+'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.+'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.+'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.12.+'
    implementation 'joda-time:joda-time:2.10.+'
    implementation 'org.codehaus.groovy:groovy-all:3.0.7'

    implementation group: 'org.antlr', name: 'antlr4', version: '4.8-1'
    implementation group: 'org.antlr', name: 'antlr4-runtime', version: '4.8-1'
    implementation group: 'org.antlr', name: 'antlr4-annotations', version: '4.3'

    antlr 'org.antlr:antlr4:4.8-1'

    testImplementation group: 'org.antlr', name: 'antlr4-runtime-testsuite', version: '4.8-1'
    testImplementation group: 'cglib', name: 'cglib-nodep', version:'3.3.0'

    testImplementation group: 'org.hamcrest', name: 'hamcrest-core', version:'1.3'
    testImplementation group: 'org.objenesis', name: 'objenesis', version:'2.6'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.28.+'
    testImplementation group: 'org.powermock', name: 'powermock-core', version: '2.0.9'
    testImplementation group: 'org.powermock', name: 'powermock-classloading-xstream', version:'2.0.9'
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version:'2.0.9'
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4-common', version:'2.0.9'
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4-rule', version:'2.0.9'
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'

    testImplementation('org.spockframework:spock-core:2.0-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.2')

    // you can use testRuntimeClasspath if you don't want to use spock-report-specific features in your Specs
    testImplementation( 'com.athaydes:spock-reports:2.0-groovy-3.0' ) {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

jacocoTestReport {
    reports {
        xml.required = true // coveralls plugin depends on xml format report
        html.required = true
    }
}

check.dependsOn jacocoTestReport
